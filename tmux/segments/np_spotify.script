#!/usr/bin/env osascript
# Returns the current playing song in Spotify for OSX
tell application "System Events"
  set process_list to (name of every process)
end tell

if process_list contains "Spotify" then
  tell application "Spotify"
    if player state is playing or player state is paused then
      set track_name to name of current track
      set artist_name to artist of current track

      if length of artist_name is greater than 5 then
        set artist_name_parts to my explode(" ", artist_name)
        set artist_name to last item of artist_name_parts
      end if

      set trim_length to 20
      if player state is playing then
        set now_playing to "► " & artist_name & " - " & track_name
      else
        set now_playing to "❙❙ " & artist_name & " - " & track_name
      end if
      if length of now_playing is less than trim_length then
        set now_playing_trim to now_playing
      else
        set now_playing_trim to characters 1 thru trim_length of now_playing as string
      end if
    end if
  end tell
end if

on explode(delimiter, input)
  local delimiter, input, ASTID
  set ASTID to AppleScript's text item delimiters
  try
    set AppleScript's text item delimiters to delimiter
    set input to text items of input
    set AppleScript's text item delimiters to ASTID
    return input --> list
  on error eMsg number eNum
    set AppleScript's text item delimiters to ASTID
    error "Can't explode: " & eMsg number eNum
  end try
end explode
